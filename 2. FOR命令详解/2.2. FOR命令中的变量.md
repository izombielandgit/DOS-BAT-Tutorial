## 2.2. FOR命令中的变量

先把FOR的变量全部列出来：

1. [~I](#1-i) 删除任何引号(")，扩展 %I
2. [%~fI](#2-fi) 将 %I 扩展到一个完全合格的路径名
3. [%~dI](#3-di) 仅将 %I 扩展到一个驱动器号
4. [%~pI](#4-pi) 仅将 %I 扩展到一个路径
5. [%~nI](#5-ni) 仅将 %I 扩展到一个文件名
6. [%~xI](#6-xi) 仅将 %I 扩展到一个文件扩展名
7. [%~sI](#7-si) 扩展的路径只含有短名
8. [%~aI](#8-ai) 将 %I 扩展到文件的文件属性
9. [%~tI](#9-ti) 将 %I 扩展到文件的日期/时间
10. [%~zI](#10-zi) 将 %I 扩展到文件的大小
11. [%~$PATH:I](#11-pathi) 查找列在路径环境变量的目录，并将 %I 扩展到找到的第一个完全合格的名称。如果环境变量名未被定义，或者没有找到文件，此组合键会扩展到空字符串

我们可以看到每行都有一个大写字母“I”,这个I其实就是我们在FOR带入的变量,我们FOR语句代入的变量名是什么,这里就写什么.

比如:

```
FOR /F %%z IN ('set') DO @echo %%z
```

这里我们代入的变量名是z那么我们就要把那个I改成z，例如`%~fI`改为`%~fz`
至于前面的`%~p`这样的内容就是语法了！

### 1. ~I

删除任何引号(")，扩展 %I

这个变量的作用就如他的说明，删除引号！

我们来看这个例子：

首先建立临时文件temp.txt，内容如下：

```
"1111
"2222"
3333"
"4444"44
"55"55"55
```

建立一个BAT文件代码如下：

```
@echo off
echo ^"1111>temp.txt
echo "2222">>temp.txt
echo 3333^">>temp.txt
echo "4444"44>>temp.txt
echo ^"55"55"55>>temp.txt
rem 上面建立临时文件，注意不成对的引号要加转义字符^，重定向符号前不要留空格
FOR /F "delims=" %%i IN (temp.txt) DO echo %%~i
pause
del temp.txt
```

执行后，我们看CMD的回显如下：

```
1111      #字符串前的引号被删除了
2222      #字符串首尾的引号都被删除了
3333"     #字符串前无引号，后面的引号保留
4444"44   #字符串前面的引号删除了，而中间的引号保留
55"55"55  #字符串前面的引号删除了，而中间的引号保留
请按任意键继续...
```

和之前temp.txt中的内容对比一下，我们会发现第1、2、4、5行的引号都消失了，这就是删除引号~i的作用了！

删除引号规则如下：

1. 若字符串首尾同时存在引号，则删除首尾的引号；
2. 若字符串尾不存在引号，则删除字符串首的引号；
3. 如果字符串中间存在引号，或者只在尾部存在引号，则不删除。

### 2. %~fI

将 %I 扩展到一个完全合格的路径名

例：

把test.bat保存放在D:\，内容如下：

```
@echo off
FOR /F "delims==" %%i IN ('dir /b') DO @echo %%~fi
pause
```

执行后显示内容如下：

```
D:\test.bat
请按任意键继续...
```

当我把代码中的`%~fi`直接改成`%i`

```
@echo off
FOR /F "delims==" %%i IN ('dir /b') DO @echo %%i
pause
```

执行后就会显示以下内容：

```
test.bat
请按任意键继续...
```

通过对比，我们很容易就看出没有路径了，这就是“将 %I 扩展到一个完全合格的路径名”的作用。

也就是如果`%i`变量的内容是一个文件名的话，他就会把这个文件所在的绝对路径打印出来，而不只单单打印一个文件名，自己动手动实验下就知道了。

### 3. %~dI

仅将 %I 扩展到一个驱动器号

例：

代码如下，放到桌面执行。

```
@echo off
FOR /F "delims==" %%i IN ('dir /b') DO @echo %%~di
pause
```

执行后CMD里显示如下：

```
C:
请按任意键继续...
```

桌面就一个文件test.bat，`%%~di`作用是，如果变量`%%i`的内容是一个文件或者目录名，他就会把他这文件或者目录所在的盘符号打印出来。

### 4. %~pI

仅将 %I 扩展到一个路径

这个用法和上面一样，他只打印路径不打印文件名字。

```
@echo off
FOR /F "delims==" %%i IN ('dir /b') DO @echo %%~pi
pause
```

自己复制代码看结果，下面几个都是这么个用法，代码给出来，大家自己看结果吧。

### 5. %~nI

仅将 %I 扩展到一个文件名

只打印文件名字：

```
@echo off
FOR /F "delims==" %%i IN ('dir /b') DO @echo %%~ni
pause
```

### 6. %~xI

仅将 %I 扩展到一个文件扩展名

只打印文件的扩展名：

```
@echo off
FOR /F "delims==" %%i IN ('dir /b') DO @echo %%~xi
pause
```

### 7. %~sI

扩展的路径只含有短名

打印绝对路径短文件名（短路径名）：

```
@echo off
FOR /F "delims==" %%i IN ('dir /b') DO @echo %%~si
pause
```

### 8. %~aI

将 %I 扩展到文件的文件属性

打印文件的属性：

```
@echo off
FOR /F "delims==" %%i IN ('dir /b') DO @echo %%~ai
pause
```

### 9. %~tI

将 %I 扩展到文件的日期/时间

打印文件建立的日期：

```
@echo off
FOR /F "delims==" %%i IN ('dir /b') DO @echo %%~ti
pause
```

### 10. %~zI

将 %I 扩展到文件的大小

打印文件的大小：

```
@echo off
FOR /F "delims==" %%i IN ('dir /b') DO @echo %%~zi
pause
```

补充：上面例子中的"delims=="可以改为"delims="，即不要分隔符。

### 11. %~$PATH:I

查找列在路径环境变量的目录，并将 %I 扩展到找到的第一个完全合格的名称。如果环境变量名未被定义，或者没有找到文件，此组合键会扩展到空字符串

这是最后一个，和上面那些都不一样。

例：

```
@echo off
FOR /F "delims=" %%i IN ("notepad.exe") DO echo %%~$PATH:i
pause
```

上面代码显示结果为

```
C:\Windows\System32\notepad.exe
请按任意键继续...
```

意思是在PATH变量里指定的路径里搜索notepad.exe文件，如果有notepad.exe则会把他所在绝对路径打印出来，没有就打印一个错误！