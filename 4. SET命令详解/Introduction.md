# 4. SET命令详解

可以使用`set /?`查看SET的帮助。

在上一章中简单的介绍了一下SET设置自定义变量的作用，现在具体讲一下SET的功能。

* [4.1. 用SET命令设置自定义变量](#41-用set命令设置自定义变量)
* [4.2. 用SET命令进行简单计算](#42-用set命令进行简单计算)
* [4.3. 用命令进行字符串处理](#43-用命令进行字符串处理)
   * [4.3.1. 字符串替换](#431-字符串替换)
   * [4.3.2. 字符串截取](#432-字符串截取)

## 4.1. 用SET命令设置自定义变量

显示、设置或删除 cmd.exe 环境变量。

```
SET [variable=[string]]
```

**variable** 指定环境变量名。

**string** 指定要指派给变量的一系列字符串。

要显示当前环境变量，键入不带参数的SET。

SET命令不允许变量名含有等号。

例：

```
@echo off
set var=我是值
echo %var%
pause
```

请看`set var=我是值`，这就是在批处理中设置变量的方法。

`set`是命令，`var`是变量名，=号右边的`我是值`是变量的值。

在批处理中我们要引用这个量就把变量名用两个%（百分号）扩起来,如`%var%`

SET还可以提供一个交互界面，让用户自己输入变量的值，然后再根据这个值来做相应操作，SET的这种语法，只需要加一个`/P`参数就可以了。

```
SET /P variable=[promptString]
```

例：

```
@echo off
set /p var=请输入变量的值:
echo 您输入的值是:%var%
pause
```

`set /p`是命令语法，`var`是变量名，=号右边的`请输入变量的值:`是提示语，不是变量的值。

运行后，我们在提示语后面直接输入`1`，显示结果：

```
请输入变量的值:1
您输入的值是:1
请按任意键继续...
```

## 4.2. 用SET命令进行简单计算

语法：

```
SET /A expression
```

`/A`命令行开关指定等号右边的字符串为被评估的数字表达式。该表达式
评估器很简单并以递减的优先权顺序支持下列操作：

```
()                  - 分组
! ~ -               - 一元运算符
* / %               - 算数运算符
+ -                 - 算数运算符
<< >>               - 逻辑移位
&                   - 按位“与”
^                   - 按位“异”
|                   - 按位“或”
= *= /= %= += -=    - 算数赋值
&= ^= |= <<= >>=    - 二进制运算赋值
,                   - 表达式分隔符
```

下面简单解释一下：

SET的`/A`参数就是让SET可以支持数学符号进行加减等一些数学运算。

例：

```
@echo off
set /p input=请输入计算表达式:
set /a var=%input%
echo 计算结果:%input%=%var%
pause
```

注意：**DOS计算只能精确到整数**

请看下面几个运算过程：

```
请输入计算表达式:1+9+20+30-10
计算结果:1+9+20+30-10=50
请按任意键继续...
```

```
请输入计算表达式:10/3
计算结果:10/3=3  #DOS计算精确到整数，小数舍了。
请按任意键继续...
```

```
请输入计算表达式:-100+62
计算结果:-100+62=-38
请按任意键继续...
```

```
请输入计算表达式:100%3  #求余数
计算结果:100%3=1
请按任意键继续...
```

```
请输入计算表达式:(25+75)*2/(15+5)
计算结果:(25+75)*2/(15+5)=10
请按任意键继续...
```

```
请输入计算表达式:1234567890*9876543210
无效数字。数字精确度限为 32 位。
计算结果:1234567890*9876543210=
请按任意键继续...
```

注意：上面的计算过程显示，DOS计算只能精确到32位，这个32位是指二进制32位，其中最高位为符号位（0为正，1为负），低位31位为数值。31个1换成十进制为2147483647，所以DOS计算的有效值范围是-2147483647至2147483647，超出该数值范围时计算出错，请看下面的计算过程：

```
请输入计算表达式:2147483647-1  #最大值减1，值有效
计算结果:2147483647-1=2147483646
请按任意键继续...
```

运行：

```
set /a a=1+1,b=2+1,c=3+1
```

后会显示一个`4`（笔者注：好像打开批处理文件不能显示`4`，需要在CMD窗口直接输入命令来观察）。但我们用`echo %a% %b% %c%`后看结果，会发现其他数学运算也有效果，这就是逗号`,`的作用。

有时候我们需要直接在原变量进行加减操作就可以用这种语法：

```
set /a var+=1
```

这样的语法对应原始语法就是：

```
set /a var = %var% + 1
```

都是一样的结果，以原变量的值为初始值再进行数学运算，不过这样写简单一点。只要帮助里有这个语法，都可以这样用，比如：`set /a var*=2`。

另外还有一些用逻辑或取余操作符，这些符号，按照上面的使用方法会报错的。

比如我们在CMD里输入`set /a var=1 & 1`（与运算），并不会显示为`1`，而是报错。

为什么？对于这样的“逻辑或取余操作符”，我们需要把他们用双引号`"`引起来，也可以用转义字符`^`，例：

与运算：

```
@echo off
set /a var= 1 "&" 1
echo %var%
pause
```

其他逻辑或取余操作符用法：

异运算：

```
set /a var= 1 "^" 1
```

取模运算：（笔者注：批处理文件中`"%"`要写为`"%%"`）

```
set /a var= 1 "%" 1
```

左移位运算，3的二进制为11，左移2位为1100，换成十进制就是12：

```
set /a var= 3 "<<" 2
```

右移位运算，4 的二进制为 100，右移动 2 位为 1，结果为1：

```
set /a var= 4 ">>" 2
```

凡是按位计算均需换算成二进制，下面行中的符号均针对二进制这些符号也可以用 `&=` `^=` `|=` `<<=` `>>=`。这样的简单用法，如：（注意引号）

```
set /a var"&=" 1
```

相当于：

```
set /a var = %var% "&" 1
```

思考题：求2的n次方

参考答案：

```
@echo off
set /p n=请输入2的几次方:
set /a num=1^<^<n
echo %num%
pause
```

（`set /?`有一段描述：在表达式中的任何非数字字符串键作为环境变量名称，这些环境变量名称的值已在使用前转换成数字。如果指定了一个环境变量名称，但未在当前环境中定义，那么值将被定为零。这使你可以使用环境变量值做计算而不用键入那些 % 符号来得到它们的值。即是此例中`set /a num=1^<^<n`的`n`不需要写成`%n%`。）

或：

```
@echo off
set /p n=请输入2的几次方:
set /a num=1 "<<" n
echo 2的%n%次方为:%num%
pause
```

## 4.3. 用命令进行字符串处理

### 4.3.1. 字符串替换

语法：

```
%PATH:str1=str2%
```

意思是：将字符串变量`%PATH%`中的`str1`替换为`str2`

这个是替换变量值的内容，看例子：

```
@echo off
set a= www. google. com
echo 替换前的值:"%a%"
set var=%a: =%
echo 替换后的值:"%var%"
pause
```

运行显示：

```
替换前的值:" www. google. com"
替换后的值:"www.google.com"
请按任意键继续...
```

对比一下，发现它是把变量`%a%`的空格给替换掉了，从这个例子，我们就可以发现`%PATH:str1=str2%`这个操作就是把变量`%PATH%`的里的`str1`全部用`str2`替换。

把上面的例子改成这样：

```
@echo off
set a=www.google.com
echo 替换前的值:"%a%"
set var=%a:.=测试%
echo 替换后的值:"%var%"
pause
```

运行显示：

```
替换前的值:"www.google.com"
替换后的值:"www测试google测试com"
请按任意键继续...
```

`set`是命令，`var`是变量名，`a`是要进行字符替换的变量的值，`.`为要替换的值，`测试`为替换后的值。

执行后就会把变量`%a%`里面的`.`全部替换为`测试`。

这就是SET的替换字符功能。

### 4.3.2. 字符串截取

语法：

```
%a:~[m[,n]]%
```

方括号`[]`表示可选，`%`为变量标识符，`a`为变量名（不可少），冒号`:`用于分隔变量名和说明部分，符号`~`可以简单理解为“偏移”即可，m为偏移量（缺省为0），n为截取长度（缺省为全部）。

`%PATH:~10,5%` 解释看例1。

例1：

```
@echo off
set a=www.google.com
set var=%a:~3,2%
echo %var%
pause
```

运行结果：

```
.g
请按任意键继续...
```

分析`set var=%a:~3,2%`：

`set`是命令，`var`是变量值，`a`是要进行字符操作的变量，`3`是从变量`a`的第几位开始显示（首位从`0`开始计算），`2`表示显示几位。

合起来就是把变量`a`的值从第4位（偏移量3）开始,把2个字符赋予给变量`var`。

其他两种语法

```
%PATH:~-10%
%PATH:~0,-2%
```

`%PATH:~-10%` 解释看例2，例3。

例2：

```
@echo off
set a=www.google.com
set var=%a:~-4%
echo %var%
pause
```

运行结果：

```
.com
请按任意键继续...
```

这个就是把变量`a`倒数`4`位的值给变量`var`。

例3：

```
@echo off
set a=www.google.com
set var=%a:~3%
echo %var%
pause
```

运行结果：

```
.google.com
请按任意键继续...
```

这个就是把变量`a`从第`3`位开始后面全部的值给变量`var`。

`%PATH:~0,-2%` 解释看例4，例5。

例4：

```
@echo off
set a=www.google.com
set var=%a:~0,-4%
echo %var%
pause
```

运行结果：

```
www.google
请按任意键继续...
```

从结果分析，分析出：这是把变量`a`的值从`0`位开始，到倒数第`4`位之前的值全部赋予给`var`。

如果改成这样，例5：

```
@echo off
set a=www.google.com
set var=%a:~2,-3%
echo %var%
pause
```

运行结果：

```
w.google.
请按任意键继续...
```

就是显示从第3位（偏移量2）开始减去倒数三位字符的值赋给变量`var`。

小结如下：

```
a=www.google.com

%a:~3,2%  = ".g"           #偏移量3，从第四位向右取2位
%a:~-4%   = ".com"         #偏移量-4，截取倒数4位（也可理解为从右边开始截取4位）
%a:~3%    = ".google.com"  #偏移量3，可理解为去掉左边3位，右取全部
%a:~0,-4% = "www.google"   #偏移量0，右取长度至-4，即倒数4位
%a:~2,-3% = "w.google."    #偏移量2，右取长度至-3，即倒数3位
```

上面所述用法其实相当于 vbs 函数 mid、left、right

```
%a:~0,n%  相当于函数left(a,n)，取左边n位
%a:~-m%   相当于函数right(a,m)，取右边m位
%a:~m,n%  相当于函数mid(a,m+1,n)，从m+1位开始取n位
%a:~m,-n% 相当于函数mid(a,m+1,len(a)-m-n)
%a:~m %   相当于函数mid(a,m+1,len(a)-m)或者right(a,len(a)-m)
```

思考题：输入任意字符串，求字符串的长度

参考答案：

```
@echo off
set /p str=请输入任意长度的字符串:
echo 你输入了字符串:"%str%"
if not defined str (pause & goto :eof)
set num=0
:len
set /a num+=1
set str=%str:~0,-1%     #也可以%str:~1%
if defined str goto len
echo 字符串长度为：%num%
pause
```